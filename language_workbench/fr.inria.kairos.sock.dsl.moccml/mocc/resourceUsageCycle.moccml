AutomataConstraintLibrary resourceUsageCycle {
	
	import "platform:/plugin/fr.inria.aoste.timesquare.ccslkernel.model/ccsllibrary/kernel.ccslLib" as kernel;
	import "platform:/plugin/fr.inria.aoste.timesquare.ccslkernel.model/ccsllibrary/CCSL.ccslLib" as CCSLLib;

	RelationLibrary ResourceUsageCycleLibrary {
		
		AutomataRelationDefinition ResourceUsageCycleRelationDefinition [ResourceUsageCycleRelation] {
			
			/* 
			 * TRANSITIONS DEFINITION
			 */ 
			
			init: Start
			
			from Start to Free : starting
			-> ()
			
			from Free to Busy : enter
			-> (when ResourceUsageCycleIsEnteredEvent)
			
			from Free to Free : idle
			-> (when ResourceUsageCycleIdleEvent)
			
			from Busy to Free : exitNotPriority
			-> (when ResourceUsageCycleIsExitedByNotPriorityActorEvent)
			
			from Busy to Cleaning : exitPriority
			-> (when ResourceUsageCycleIsExitedByPriorityActorEvent)
			
			from Busy to Busy : isProcessed
			-> (when ResourceUsageCycleIsProcessedEvent)
			
			from Busy to Busy : exitOfAndEnterIn
			-> (when ResourceusageCycleExitOfAndEnterInEvent)
			
			from Cleaning to Free : clean
			-> (when ResourceusageCycleCleanEvent)
			
			/* 
			 * STATES DEFINITION
			 */ 
			
			State Start (
				out: starting
			)
			
			State Free (
				in: starting, idle, exitNotPriority, clean
				out: enter, idle
			)
			
			State Busy (
				in: enter, isProcessed, exitOfAndEnterIn
				out: exitNotPriority, isProcessed, exitOfAndEnterIn, exitPriority
			)
			
			State Cleaning (
				in: exitPriority
				out: clean
			)
			
		}
		RelationDeclaration ResourceUsageCycleRelation(
			ResourceUsageCycleIsEnteredEvent: clock,
			ResourceUsageCycleIsExitedByNotPriorityActorEvent: clock,
			ResourceUsageCycleIsExitedByPriorityActorEvent: clock,
			ResourceUsageCycleIsProcessedEvent: clock,
			ResourceUsageCycleIdleEvent: clock,
			ResourceusageCycleExitOfAndEnterInEvent: clock,
			ResourceusageCycleCleanEvent: clock
		)
	}

}	